defmodule ToolboxWeb.PackageLiveTest do
  use ToolboxWeb.ConnCase, async: true
  import Phoenix.LiveViewTest
  import Helpers

  use Oban.Testing, repo: Toolbox.Repo

  alias Toolbox.{Category, Packages}

  describe "Package Live View" do
    setup context do
      params =
        if Map.get(context, :package_attrs) do
          Map.get(context, :package_attrs)
        else
          []
        end

      {:ok, package} = create(:package, params)
      {:ok, _} = create(:hexpm_snapshot, package_id: package.id)

      [package: package]
    end

    defp create_package_with_many_versions(count) do
      {:ok, package} = create(:package)

      releases =
        for n <- count..1//-1,
            do: %{
              "version" => "#{n}.0.0",
              "inserted_at" => "2025-05-29T16:57:22.358745Z"
            }

      {:ok, _} =
        Packages.create_hexpm_snapshot(%{
          package_id: package.id,
          data: %{
            "meta" => %{"description" => "A pure Elixir HTTP server"},
            "downloads" => %{"recent" => 1_000},
            "docs_html_url" => "https://hexdocs.pm/bandit/",
            "releases" => releases,
            "inserted_at" => "2020-11-05T17:11:46.440731Z",
            "latest_version" => "#{count}.0.0",
            "latest_stable_version" => "#{count}.0.0"
          }
        })

      Packages.get_package_by_name(package.name)
    end

    test "mounts successfully", %{conn: conn, package: package} do
      Packages.update_package_owners(package, %{
        hexpm_owners_sync_at: DateTime.utc_now(),
        hexpm_owners: [%{email: "test@example.com", username: "owner"}]
      })

      Packages.update_package_latest_stable_version(package, %{
        hexpm_latest_stable_version_data: %{
          published_at: DateTime.utc_now(),
          published_by_username: "username",
          version: "1.7.0"
        }
      })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert page_title(view) =~ package.name
      assert has_element?(view, "[data-test-package-name]", package.name)
      assert has_element?(view, "[data-test-package-description]", package.description)

      assert all_enqueued(worker: Toolbox.Workers.HexpmWorker) == []
    end

    test "mounts successfully when missing external data", %{conn: conn, package: package} do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert page_title(view) =~ package.name
      assert has_element?(view, "[data-test-package-name]", package.name)
      assert has_element?(view, "[data-test-package-description]", package.description)

      [version_job, owner_job] = all_enqueued(worker: Toolbox.Workers.HexpmWorker)

      assert version_job.args == %{
               "action" => "get_latest_stable_version",
               "name" => package.name,
               "version" => "1.7.0"
             }

      assert owner_job.args == %{
               "action" => "get_package_owners",
               "name" => package.name
             }
    end

    test "does not enqueue get_version_downloads until the section is expanded", %{
      conn: conn,
      package: package
    } do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      refute_enqueued(
        worker: Toolbox.Workers.HexpmWorker,
        args: %{action: "get_version_downloads"}
      )

      render_click(view, "expand_version_downloads")

      assert_enqueued(
        worker: Toolbox.Workers.HexpmWorker,
        queue: :hexpm,
        args: %{action: "get_version_downloads", name: package.name, offset: 0}
      )
    end

    test "'show more' requests the next offset and appends", %{conn: conn} do
      package = create_package_with_many_versions(8)

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      render_click(view, "expand_version_downloads")

      Phoenix.PubSub.broadcast(Toolbox.PubSub, "package_live:#{package.name}", %{
        action: :refresh_version_downloads,
        offset: 0,
        version_downloads: Enum.map(1..5, &%{version: "#{&1}.0.0", downloads: 10})
      })

      render(view)

      render_click(view, "show_more_version_downloads")

      assert_enqueued(
        worker: Toolbox.Workers.HexpmWorker,
        args: %{action: "get_version_downloads", name: package.name, offset: 5}
      )

      Phoenix.PubSub.broadcast(Toolbox.PubSub, "package_live:#{package.name}", %{
        action: :refresh_version_downloads,
        offset: 5,
        version_downloads: Enum.map(6..8, &%{version: "#{&1}.0.0", downloads: 5})
      })

      html = render(view)

      for n <- 1..8 do
        assert html =~ "#{n}.0.0"
      end
    end

    test "shows a failure state after a stuck first page, and retrying clears it", %{
      conn: conn,
      package: package
    } do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      render_click(view, "expand_version_downloads")
      refute has_element?(view, "[data-test-version-downloads-failed]")

      send(view.pid, {:version_downloads_timeout, 0})
      render(view)

      assert has_element?(view, "[data-test-version-downloads-failed]")

      render_click(view, "expand_version_downloads")

      refute has_element?(view, "[data-test-version-downloads-failed]")
      assert has_element?(view, "[data-test-version-downloads-loading]")
    end

    test "a failed 'show more' shows the inline error without wiping already-revealed data", %{
      conn: conn
    } do
      package = create_package_with_many_versions(8)

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      render_click(view, "expand_version_downloads")

      Phoenix.PubSub.broadcast(Toolbox.PubSub, "package_live:#{package.name}", %{
        action: :refresh_version_downloads,
        offset: 0,
        version_downloads: Enum.map(1..5, &%{version: "#{&1}.0.0", downloads: 10})
      })

      render(view)
      render_click(view, "show_more_version_downloads")

      send(view.pid, {:version_downloads_timeout, 5})
      html = render(view)

      refute has_element?(view, "[data-test-version-downloads-failed]")
      assert has_element?(view, "[data-test-version-downloads-more-failed]")
      assert html =~ "1.0.0"
      assert html =~ "5.0.0"
    end

    test "ignores a broadcast for an offset this session isn't waiting on", %{
      conn: conn,
      package: package
    } do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      render_click(view, "expand_version_downloads")

      # Some other session's "show more" click, on the same package.
      Phoenix.PubSub.broadcast(Toolbox.PubSub, "package_live:#{package.name}", %{
        action: :refresh_version_downloads,
        offset: 5,
        version_downloads: [%{version: "9.9.9", downloads: 1}]
      })

      html = render(view)

      refute html =~ "9.9.9"
      assert has_element?(view, "[data-test-version-downloads-loading]")
    end

    test "falls back to the first version when latest_stable_version is nil", %{
      conn: conn,
      package: package
    } do
      {:ok, _} =
        Packages.create_hexpm_snapshot(%{
          package_id: package.id,
          data: %{
            "meta" => %{"description" => "A pure Elixir HTTP server"},
            "downloads" => %{"recent" => 1000},
            "docs_html_url" => "https://hexdocs.pm/bandit/",
            "releases" => [
              %{
                "version" => "2.0.0-rc.1",
                "url" => "https://hex.pm/api/packages/bandit/releases/2.0.0-rc.1",
                "has_docs" => true,
                "inserted_at" => "2025-05-29T16:57:22.358745Z"
              }
            ],
            "inserted_at" => "2020-11-05T17:11:46.440731Z",
            "latest_version" => "2.0.0-rc.1",
            "latest_stable_version" => nil
          }
        })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "h2", "Version 2.0.0-rc.1")
    end

    test "does not enqueue a get_latest_stable_version job when latest_stable_version is nil",
         %{conn: conn, package: package} do
      {:ok, _} =
        Packages.create_hexpm_snapshot(%{
          package_id: package.id,
          data: %{
            "meta" => %{"description" => "A pure Elixir HTTP server"},
            "downloads" => %{"recent" => 1000},
            "docs_html_url" => "https://hexdocs.pm/bandit/",
            "releases" => [
              %{
                "version" => "2.0.0-rc.1",
                "url" => "https://hex.pm/api/packages/bandit/releases/2.0.0-rc.1",
                "has_docs" => true,
                "inserted_at" => "2025-05-29T16:57:22.358745Z"
              }
            ],
            "inserted_at" => "2020-11-05T17:11:46.440731Z",
            "latest_version" => "2.0.0-rc.1",
            "latest_stable_version" => nil
          }
        })

      {:ok, _view, _html} = live(conn, ~p"/packages/#{package.name}")

      refute_enqueued(
        worker: Toolbox.Workers.HexpmWorker,
        args: %{action: "get_latest_stable_version"}
      )
    end

    test "handles invalid version gracefully", %{conn: conn, package: package} do
      assert_raise ToolboxWeb.PackageLive.HexpmVersionNotFoundError, fn ->
        live(conn, ~p"/packages/#{package.name}/invalid_version")
      end
    end

    test "displays package owners section when owners exist", %{conn: conn, package: package} do
      owners = [
        %{email: "jose.valim@example.com", username: "josevalim"},
        %{email: "chris.mccord@example.com", username: "chrismccord"},
        %{email: "andrea.leopardi@example.com", username: "whatyouhide"}
      ]

      Packages.update_package_owners(package, %{
        hexpm_owners_sync_at: DateTime.utc_now(),
        hexpm_owners: owners
      })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "[data-test-package-owners-section]")
      assert has_element?(view, "[data-test-owner-chip='josevalim']")
      assert has_element?(view, "[data-test-owner-chip='chrismccord']")
      assert has_element?(view, "[data-test-owner-chip='whatyouhide']")
    end

    test "displays first 4 owners as chips", %{conn: conn, package: package} do
      owners = [
        %{email: "jose.valim@example.com", username: "josevalim"},
        %{email: "chris.mccord@example.com", username: "chrismccord"},
        %{email: "andrea.leopardi@example.com", username: "whatyouhide"},
        %{email: "michał.muskała@example.com", username: "michalmuskala"}
      ]

      Packages.update_package_owners(package, %{
        hexpm_owners_sync_at: DateTime.utc_now(),
        hexpm_owners: owners
      })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "[data-test-owner-chip='josevalim']")
      assert has_element?(view, "[data-test-owner-chip='chrismccord']")
      assert has_element?(view, "[data-test-owner-chip='whatyouhide']")
      assert has_element?(view, "[data-test-owner-chip='michalmuskala']")
      refute has_element?(view, "[data-test-owners-show-more-button]")
    end

    test "shows 'show more' button when more than 4 owners exist", %{conn: conn, package: package} do
      owners = [
        %{email: "jose.valim@example.com", username: "josevalim"},
        %{email: "chris.mccord@example.com", username: "chrismccord"},
        %{email: "andrea.leopardi@example.com", username: "whatyouhide"},
        %{email: "michał.muskała@example.com", username: "michalmuskala"},
        %{email: "wojtek.mach@example.com", username: "wojtekmach"},
        %{email: "devon.estes@example.com", username: "devonestes"}
      ]

      Packages.update_package_owners(package, %{
        hexpm_owners_sync_at: DateTime.utc_now(),
        hexpm_owners: owners
      })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "[data-test-owner-chip='josevalim']")
      assert has_element?(view, "[data-test-owner-chip='chrismccord']")
      assert has_element?(view, "[data-test-owner-chip='whatyouhide']")
      assert has_element?(view, "[data-test-owner-chip='michalmuskala']")
      refute has_element?(view, "[data-test-owner-chip='wojtekmach']")
      refute has_element?(view, "[data-test-owner-chip='devonestes']")
      assert has_element?(view, "[data-test-owners-show-more-button]")
      assert element(view, "[data-test-owners-show-more-button]") |> render() =~ "+ 2 owners"
    end

    test "toggles owners popover when clicking show more button", %{conn: conn, package: package} do
      owners = [
        %{email: "jose.valim@example.com", username: "josevalim"},
        %{email: "chris.mccord@example.com", username: "chrismccord"},
        %{email: "andrea.leopardi@example.com", username: "whatyouhide"},
        %{email: "michał.muskała@example.com", username: "michalmuskala"},
        %{email: "wojtek.mach@example.com", username: "wojtekmach"},
        %{email: "devon.estes@example.com", username: "devonestes"}
      ]

      Packages.update_package_owners(package, %{
        hexpm_owners_sync_at: DateTime.utc_now(),
        hexpm_owners: owners
      })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      # Initially popover should not be visible
      refute has_element?(view, "[data-test-owners-popover]")

      # Click the show more button
      view
      |> element("[data-test-owners-show-more-button]")
      |> render_click()

      # Popover should now be visible with additional owners
      assert has_element?(view, "[data-test-owners-popover]")
      assert has_element?(view, "[data-test-owners-popover-content]")

      # Additional owners should be visible in popover
      assert has_element?(view, "[data-test-owners-popover] [data-test-owner-chip='wojtekmach']")
      assert has_element?(view, "[data-test-owners-popover] [data-test-owner-chip='devonestes']")
    end

    test "hides owners popover when clicking show more button again", %{
      conn: conn,
      package: package
    } do
      owners = [
        %{email: "jose.valim@example.com", username: "josevalim"},
        %{email: "chris.mccord@example.com", username: "chrismccord"},
        %{email: "andrea.leopardi@example.com", username: "whatyouhide"},
        %{email: "michał.muskała@example.com", username: "michalmuskala"},
        %{email: "wojtek.mach@example.com", username: "wojtekmach"}
      ]

      Packages.update_package_owners(package, %{
        hexpm_owners_sync_at: DateTime.utc_now(),
        hexpm_owners: owners
      })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      # Show popover
      view
      |> element("[data-test-owners-show-more-button]")
      |> render_click()

      assert has_element?(view, "[data-test-owners-popover]")

      # Click again to hide popover
      view
      |> element("[data-test-owners-show-more-button]")
      |> render_click()

      refute has_element?(view, "[data-test-owners-popover]")
    end

    test "package owners section works with empty owners list", %{conn: conn, package: package} do
      Packages.update_package_owners(package, %{
        hexpm_owners_sync_at: DateTime.utc_now(),
        hexpm_owners: []
      })

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "[data-test-package-owners-section]")
      refute has_element?(view, "[data-test-owner-chip]")
      refute has_element?(view, "[data-test-owners-show-more-button]")
    end

    @tag package_attrs: [name: "tower"]
    test "displays community section when package has resources", %{conn: conn, package: package} do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, data_test_attr(:community_section))
      refute has_element?(view, data_test_attr(:community_section_empty))
    end

    test "does not display community section when package doesn't have resources", %{
      conn: conn,
      package: package
    } do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, data_test_attr(:community_section))
      assert has_element?(view, data_test_attr(:community_section_empty))
    end

    @category Category.find_by_name("Actors")

    @tag package_attrs: [category: @category]
    test "displays related packages section when package has a category with more than one package",
         %{conn: conn} do
      {:ok, package} = create(:package, category: @category)
      {:ok, _} = create(:hexpm_snapshot, package_id: package.id)

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, data_test_attr(:related_packages_section))
      assert has_element?(view, data_test_attr(:related_packages_count, "1"))
    end

    @tag package: [category: @category]
    test "does not display related packages section when category has exactly one element", %{
      conn: conn,
      package: package
    } do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      refute has_element?(view, data_test_attr(:related_packages_section))
    end

    test "does not display related packages section when package has no category", %{
      conn: conn,
      package: package
    } do
      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      refute has_element?(view, data_test_attr(:related_packages_section))
    end

    test "shows documentation link when version has_docs is true", %{conn: conn, package: package} do
      Packages.update_package_latest_stable_version(package, %{
        hexpm_latest_stable_version_data: %{
          published_at: DateTime.utc_now(),
          published_by_username: "username",
          version: "1.7.0",
          has_docs: true
        }
      })

      {:ok, _view, html} = live(conn, ~p"/packages/#{package.name}")

      assert html =~ "Documentation for 1.7.0"
    end

    test "does not show documentation link when version has_docs is false", %{
      conn: conn,
      package: package
    } do
      Packages.update_package_latest_stable_version(package, %{
        hexpm_latest_stable_version_data: %{
          published_at: DateTime.utc_now(),
          published_by_username: "username",
          version: "1.7.0",
          has_docs: false
        }
      })

      {:ok, _view, html} = live(conn, ~p"/packages/#{package.name}")

      assert html =~ "No documentation for 1.7.0"
    end
  end

  describe "Follow/Unfollow Package" do
    setup do
      user = create(:user)
      {:ok, package} = create(:package)
      {:ok, _} = create(:hexpm_snapshot, package_id: package.id)

      [user: user, package: package]
    end

    test "shows follow button when user is logged in and not following", %{
      conn: conn,
      user: user,
      package: package
    } do
      conn = init_test_session(conn, %{user_id: user.id})

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "button[phx-click='follow']")
      refute has_element?(view, "button[phx-click='unfollow']")
    end

    test "shows following button when user is logged in and following", %{
      conn: conn,
      user: user,
      package: package
    } do
      Toolbox.Users.follow_package(user.id, package.id)
      conn = init_test_session(conn, %{user_id: user.id})

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "button[phx-click='unfollow']")
      refute has_element?(view, "button[phx-click='follow']")
    end

    test "shows follow button and redirects to auth when user is not logged in", %{
      conn: conn,
      package: package
    } do
      _oauth_server = test_server_github_oauth()

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "button[phx-click='follow']")
      refute has_element?(view, "button[phx-click='unfollow']")

      # Clicking follow should redirect to auth
      result =
        view
        |> element("button[phx-click='follow']")
        |> render_click()

      assert {:error, {:redirect, %{to: "/auth/github"}}} = result
    end

    test "follows a package when clicking follow button", %{
      conn: conn,
      user: user,
      package: package
    } do
      conn = init_test_session(conn, %{user_id: user.id})

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "button[phx-click='follow']")

      view
      |> element("button[phx-click='follow']")
      |> render_click()

      assert has_element?(view, "button[phx-click='unfollow']")
      refute has_element?(view, "button[phx-click='follow']")

      # Verify in database
      assert Toolbox.Users.following_package?(user.id, package.id) == true
    end

    test "unfollows a package when clicking following button", %{
      conn: conn,
      user: user,
      package: package
    } do
      Toolbox.Users.follow_package(user.id, package.id)
      conn = init_test_session(conn, %{user_id: user.id})

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      assert has_element?(view, "button[phx-click='unfollow']")

      view
      |> element("button[phx-click='unfollow']")
      |> render_click()

      assert has_element?(view, "button[phx-click='follow']")
      refute has_element?(view, "button[phx-click='unfollow']")

      # Verify in database
      assert Toolbox.Users.following_package?(user.id, package.id) == false
    end

    test "can follow and unfollow multiple times", %{conn: conn, user: user, package: package} do
      conn = init_test_session(conn, %{user_id: user.id})

      {:ok, view, _html} = live(conn, ~p"/packages/#{package.name}")

      # Follow
      view
      |> element("button[phx-click='follow']")
      |> render_click()

      assert has_element?(view, "button[phx-click='unfollow']")

      # Unfollow
      view
      |> element("button[phx-click='unfollow']")
      |> render_click()

      assert has_element?(view, "button[phx-click='follow']")

      # Follow again
      view
      |> element("button[phx-click='follow']")
      |> render_click()

      assert has_element?(view, "button[phx-click='unfollow']")

      # Verify final state in database
      assert Toolbox.Users.following_package?(user.id, package.id) == true
    end
  end
end
